#version 440 core

//THESE NEED TO BE IDENTICAL TO THE ONES DEFINED IN pathmarching.cpp
const uvec2 workGroupSize = uvec2(32, 32);
const uvec2 workGroupCount = uvec2(4, 4);

layout(local_size_x = 32, local_size_y = 32) in;
layout(rgba32f, binding = 0) uniform image2D hdrOut;

uniform uvec2 tileOffset;
uniform uint currentSample;
uniform uint samplesPerPass;

uniform uint uFrame;
uniform mat4 viewMat;


const float world_scale = 1000;  //how big one unit in metern is (this is for easier camera control)
//const float FAR_PLANE = 149597870700.+696342000.*10000/world_scale;
float FAR_PLANE = 30.;

const float steps = 300;

const bool preview = true;
const int max_bounce = 12;

const float focal_length = 50;  // focal length in mm
const vec3 ro = vec3(0.,0.,1.); // render origin

const float pi = 3.14159265359;

const float light_phi = pi/2;
const float light_theta = 2*pi/5;


struct mat {
    uint type; // 0 lambertian; 1 metal; 2 pure Emmiter; 3 Volumetric
    vec3 albedo;
    float fuzz;
    vec3 emmission;
//preview:
    float roughness;
    float refractionIndex;
    vec3 diff_col;
    vec3 spec_col;
};

mat lambertian(vec3 col){return mat(0, col, 0, vec3(0), 0.3, 0.3, col, vec3(1));}
mat metal(vec3 col, float fuzz){return mat(1, col, fuzz, vec3(0), 0.1, fuzz, col, col);}
mat emitter(vec3 col){return mat(2, vec3(0), 0, col, 0, 0, col, vec3(0));}

struct object {
    uint obj_index;
    float eps;
    mat3 rotation;
    vec3 scaling;
    float scale;
    vec3 position;
    mat material;
};

struct hit {
    bool hit;
    float dist;
    uint steps;
    uint index;
};

object world[] = object[](
//    object(2, 0.00001, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1000, vec3(0,0,0), mat(0, lambertian(vec3(1,1,1))),
//    object(1, 0.001, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1000, vec3(0,-1000,0), lambertian(vec3(.8,.8,.8))),
//    object(8, 0.001, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1, vec3(0,0,0), lambertian(vec3(.8,.8,.8))),
//    object(1, 0.001, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1000, vec3(0,-1000,3000), emitter(vec3(100,1.,1.)))
object(1, 0.0001, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1000, vec3(0,0,0), lambertian(vec3(.7,.3,.3)))
//    object(1, 0.001, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1000, vec3(2000,0,0), metal(vec3(.722,.451,.20), 0.3)),
//    object(1, 0.001, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1000, vec3(0,0,2000), emitter(vec3(8,4,4))),
//    object(8, 0.01, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1, vec3(0,0,0), lambertian(vec3(.8,.8,0.)))
//    object(2, 0, 0.01, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1000, vec3(0,0,2000), 0.3, 0.3, vec4(1,1,1,1), vec4(1,1,1,1)),
//    object(3, 0.001, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 157, vec3(0,0,0), lambertian(vec3(.8))),  //enterprise
//    object(1, 1, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 6371009, vec3(0,408000+12756270/2,0), lambertian(vec3(0,0,1))), //earth
//    object(1, 10, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 1737400, vec3(397000000,408000+12756270/2,0), lambertian(vec3(.8))),  //moon
//    object(1, 1000, mat3(1,0,0,0,1,0,0,0,1), vec3(1,1,1), 695700000., vec3(0,408000+12756270/2,151600000000.), emitter(vec3(10000)))  //sun
);

float atan2(in float y, in float x){
    return (x == 0.) ? sign(y)*pi/2 : atan(y, x);
}

//random functions
float rand_vec2seed(vec2 co){
    return fract(sin(dot(co, vec2(12.9898, 78.233)))*43758.5453);
}

//TODO: Aktueller Zufallsgenerator nur für große uFrame zuverlässig zufällig.
uint non_constant_seed = uint(floor(rand_vec2seed(ivec2(tileOffset * workGroupSize * workGroupCount + gl_GlobalInvocationID.xy))*1341*(uFrame+100)));
uint constant_seed = 0;

uint rot(uint x, uint k){
    return (x << k) | (x >> (32 - k));
}

void final(inout uint a, inout uint b, inout uint c){
    c ^= b;
    c -= rot(b, 14);
    a ^= c;
    a -= rot(c, 11);
    b ^= a;
    b -= rot(a, 25);
    c ^= b;
    c -= rot(b, 16);
    a ^= c;
    a -= rot(c, 4);
    b ^= a;
    b -= rot(a, 14);
    c ^= b;
    c -= rot(b, 24);
}

uint hash_uint2(uint kx, uint ky){
    uint a, b, c;
    a = b = c = 0xdeadbeefu + (2u << 2u) + 13u;
    b += ky;
    a += kx;
    final(a, b, c);
    return c;
}

uint hash_uint3(uint kx, uint ky, uint kz){
    uint a, b, c;
    a = b = c = 0xdeadbeefu + (3u << 2u) + 13u;
    c += kz;
    b += ky;
    a += kx;
    final(a, b, c);
    return c;
}

uint hash_int3(int kx, int ky, int kz){
    return hash_uint3(uint(kx), uint(ky), uint(kz));
}

float hash_uint2_to_float(uint kx, uint ky){
    return float(hash_uint2(kx, ky)) / float(0xFFFFFFFFu);
}

float hash_vec2_to_float(vec2 k){
    return hash_uint2_to_float(floatBitsToUint(k.x), floatBitsToUint(k.y));
}

void FLOORFRAC(inout float x, inout int x_int, inout float x_fract) {
    float x_floor = floor(x);
    x_int = int(x_floor);
    x_fract = x - x_floor;
}

float tri_mix(float v0, float v1, float v2, float v3, float v4, float v5,
    float v6, float v7, float x, float y, float z){
    float x1 = 1.0 - x;
    float y1 = 1.0 - y;
    float z1 = 1.0 - z;
    return z1 * (y1 * (v0 * x1 + v1 * x) + y * (v2 * x1 + v3 * x)) +
        z * (y1 * (v4 * x1 + v5 * x) + y * (v6 * x1 + v7 * x));
}

float fade(float t){
    return t * t * t * (t * (t * 6.0 - 15.0) + 10.0);
}

float negate_if(float value, uint condition){
    return (condition != 0u) ? -value : value;
}

vec3 random_vec3_offset(float seed){
    return vec3(100.0 + hash_vec2_to_float(vec2(seed, 0.0)) * 100.0,
    100.0 + hash_vec2_to_float(vec2(seed, 1.0)) * 100.0,
    100.0 + hash_vec2_to_float(vec2(seed, 2.0)) * 100.0);
}

float noise_grad(uint hash, float x, float y, float z){
    uint h = hash & 15u;
    float u = h < 8u ? x : y;
    float vt = ((h == 12u) || (h == 14u)) ? x : z;
    float v = h < 4u ? y : vt;
    return negate_if(u, h & 1u) + negate_if(v, h & 2u);
}

float noise_perlin(vec3 vec){
    int X, Y, Z;
    float fx, fy, fz;

    FLOORFRAC(vec.x, X, fx);
    FLOORFRAC(vec.y, Y, fy);
    FLOORFRAC(vec.z, Z, fz);

    float u = fade(fx);
    float v = fade(fy);
    float w = fade(fz);

    float r = tri_mix(noise_grad(hash_int3(X, Y, Z), fx, fy, fz),
    noise_grad(hash_int3(X + 1, Y, Z), fx - 1, fy, fz),
    noise_grad(hash_int3(X, Y + 1, Z), fx, fy - 1, fz),
    noise_grad(hash_int3(X + 1, Y + 1, Z), fx - 1, fy - 1, fz),
    noise_grad(hash_int3(X, Y, Z + 1), fx, fy, fz - 1),
    noise_grad(hash_int3(X + 1, Y, Z + 1), fx - 1, fy, fz - 1),
    noise_grad(hash_int3(X, Y + 1, Z + 1), fx, fy - 1, fz - 1),
    noise_grad(hash_int3(X + 1, Y + 1, Z + 1), fx - 1, fy - 1, fz - 1),
    u,
    v,
    w);

    return r;
}

float noise_scale3(float result){
    return 0.9820 * result;
}

float snoise(vec3 p){
    float r = noise_perlin(p);
    return (isinf(r)) ? 0.0 : noise_scale3(r);
}

float noise(vec3 p){
    return 0.5 * snoise(p) + 0.5;
}

float fractal_noise(vec3 p, float octaves, float roughness){
    float fscale = 1.0;
    float amp = 1.0;
    float maxamp = 0.0;
    float sum = 0.0;
    octaves = clamp(octaves, 0.0, 15.0);
    int n = int(octaves);
    for (int i = 0; i <= n; i++) {
        float t = noise(fscale * p);
        sum += t * amp;
        maxamp += amp;
        amp *= clamp(roughness, 0.0, 1.0);
        fscale *= 2.0;
    }
    float rmd = octaves - floor(octaves);
    if (rmd != 0.0) {
        float t = noise(fscale * p);
        float sum2 = sum + t * amp;
        sum /= maxamp;
        sum2 /= maxamp + amp;
        return (1.0 - rmd) * sum + rmd * sum2;
    }
    else {
        return sum / maxamp;
    }
}

void node_noise_texture_3d(vec3 co,
float w,
float scale,
float detail,
float roughness,
float distortion,
out float value,
out vec4 color
){
    vec3 p = co * scale;
    if (distortion != 0.0) {
        p += vec3(snoise(p + random_vec3_offset(0.0)) * distortion,
        snoise(p + random_vec3_offset(1.0)) * distortion,
        snoise(p + random_vec3_offset(2.0)) * distortion);
    }

    value = fractal_noise(p, detail, roughness);
    color = vec4(value,
    fractal_noise(p + random_vec3_offset(3.0), detail, roughness),
    fractal_noise(p + random_vec3_offset(4.0), detail, roughness),
    1.0);
//    return value;
}


//uint hash(){
//    uint x = ++non_constant_seed;
//    x += (x << 10u);
//    x ^= (x >> 6u);
//    x += (x << 3u);
//    x ^= (x >> 11u);
//    x += (x << 15u);
//    return x;
//}

uint hash(uint x){
    x += (x << 10u);
    x ^= (x >> 6u);
    x += (x << 3u);
    x ^= (x >> 11u);
    x += (x << 15u);
    return x;
}

int hash(int x){
    x += (x << 10);
    x ^= (x >> 6);
    x += (x << 3);
    x ^= (x >> 11);
    x += (x << 15);
    return x;
}

float floatConstruct(uint m){
    const uint ieeeMantissa = 0x007FFFFFu;
    const uint ieeeOne = 0x3F800000u;
    m &= ieeeMantissa;
    m |= ieeeOne;
    float f = uintBitsToFloat(m);
    return f - 1.;
}

float rand(){return floatConstruct(hash(++non_constant_seed));}
float rand(uint x){return floatConstruct(hash(x));}

vec3 random_in_unit_disk() {
    while (true) {
        vec3 p = vec3(2*rand()-1, 2*rand()-1, 0);
        if (length(p) >= 1) continue;
        return p;
    }
    return vec3(0);
}

vec3 rand_in_unit_sphere(){
    vec3 p;
    while (true){
        p = vec3(2*rand()-1, 2*rand()-1, 2*rand()-1);
        if (length(p) >= 1) {continue;}
        break;
    }
    return p;
}
vec3 rand_unit_sphere(){
    return normalize(rand_in_unit_sphere());
}

vec3 rand_in_hemisphere(vec3 nor){
    vec3 in_unit_sphere = rand_in_unit_sphere();
    if (dot(in_unit_sphere, nor) > 0.) return in_unit_sphere;
    return -in_unit_sphere;
}

//DE of the primitives is from https://iquilezles.org/articles/distfunctions/
float dot2( in vec2 v ) { return dot(v,v); }
float dot2( in vec3 v ) { return dot(v,v); }
float ndot( in vec2 a, in vec2 b ) { return a.x*b.x - a.y*b.y; }
float sdSphere( vec3 p, float s ){
    return length(p)-s;
}
float sdBox( vec3 p, vec3 b ){
    vec3 q = abs(p) - b;
    return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
}
float sdRoundBox( vec3 p, vec3 b, float r ){
    vec3 q = abs(p) - b;
    return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0) - r;
}
float sdPlane( vec3 p, vec3 n, float h ){
    // n must be normalized
    return dot(p,n) + h;
}
float sdInfinteCylinder(vec3 p, vec3 c){
    return length(p.xz-c.xy)-c.z;
}
float sdCappedCylinder( vec3 p, float h, float r ){
    vec2 d = abs(vec2(length(p.xz),p.y)) - vec2(h,r);
    return min(max(d.x,d.y),0.0) + length(max(d,0.0));
}
float sdCappedCylinder(vec3 p, vec3 a, vec3 b, float r){
    vec3  ba = b - a;
    vec3  pa = p - a;
    float baba = dot(ba,ba);
    float paba = dot(pa,ba);
    float x = length(pa*baba-ba*paba) - r*baba;
    float y = abs(paba-baba*0.5)-baba*0.5;
    float x2 = x*x;
    float y2 = y*y*baba;
    float d = (max(x,y)<0.0)?-min(x2,y2):(((x>0.0)?x2:0.0)+((y>0.0)?y2:0.0));
    return sign(d)*sqrt(abs(d))/baba;
}
float sdRoundedCylinder( vec3 p, float ra, float rb, float h ){
    vec2 d = vec2( length(p.xz)-2.0*ra+rb, abs(p.y) - h );
    return min(max(d.x,d.y),0.0) + length(max(d,0.0)) - rb;
}
float sdSolidAngle(vec3 p, vec2 c, float ra){
    // c is the sin/cos of the angle
    vec2 q = vec2( length(p.xz), p.y );
    float l = length(q) - ra;
    float m = length(q - c*clamp(dot(q,c),0.0,ra) );
    return max(l,m*sign(c.y*q.x-c.x*q.y));
}
float sdEllipsoid( vec3 p, vec3 r ){
    float k0 = length(p/r);
    float k1 = length(p/(r*r));
    return k0*(k0-1.0)/k1;
}

// operations
// inner (i) Operations transform the parameterspace of the primitives,
// while outer (o) Operations transform the primitive it self
vec3 iElongate(vec3 pos, vec3 h){
    return max(abs(pos)-h, 0.0);
}
float oElongate(float dist, vec3 pos, vec3 h){
    vec3 q = abs(pos)-h;
    return dist + min(max(q.x,max(q.y,q.z)),0.0);
}
// this also scales the primitve. The amount of scaling is dependend on the used distance function
float oBevel(float dist, float bevel){
    return dist - bevel;
}
float oOnion(float dist, float thickness){
    return abs(dist)-thickness;
}
float oMorph(float d1, float d2, float t){
    return (1-t)*d1+t*d2;
}
// this extrudes the pos.xy distance of an 2d DE into 3d
float oExtrusion(float dist, vec3 pos, float h){
    vec2 w = vec2(dist, abs(pos.z) - h );
    return min(max(w.x,w.y),0.0) + length(max(w,0.0));
}
// this screws the pos.xy distance of an 2d DE into 3d
vec2 iScrew(vec3 pos, float o){
    return vec2( length(pos.xz) - o, pos.y );
}

// Boolean Operations, all of these are outer operations
float bU( float d1, float d2 ) {
    return min(d1,d2);
}
float bS( float d1, float d2 ) {
    return max(-d1,d2);
}
float bI( float d1, float d2 ) {
    return max(d1,d2);
}
float bD(float d1, float d2){
    return bS(bI(d1, d2), d1);
}
float sbU( float d1, float d2, float k ) {
    float h = clamp( 0.5 + 0.5*(d2-d1)/k, 0.0, 1.0 );
    return mix( d2, d1, h ) - k*h*(1.0-h);
}
float sbS( float d1, float d2, float k ) {
    float h = clamp( 0.5 - 0.5*(d2+d1)/k, 0.0, 1.0 );
    return mix( d2, -d1, h ) + k*h*(1.0-h);
}
float sbI( float d1, float d2, float k ) {
    float h = clamp( 0.5 - 0.5*(d2-d1)/k, 0.0, 1.0 );
    return mix( d2, d1, h ) + k*h*(1.0-h);
}
float sbD(float d1, float d2, float k){
    return sbS(sbI(d1, d2, k), d1, k);
}
//Rotation and Translation
vec3 iTrans(vec3 pos, mat4 transform){
    vec4 tmp = inverse(transform)*vec4(pos, 1);
    //    if (tmp.w == 0) return vec3(tmp.x,tmp.y,tmp.z);
    return vec3(tmp.x/tmp.w,tmp.y/tmp.w,tmp.z/tmp.w);
}
//scaling http://jamie-wong.com/2016/07/15/ray-marching-signed-distance-functions/#rotation-and-translation
vec3 iScale(vec3 pos, vec3 s){
    return pos/s;
}
float oScale(float dist, vec3 s){
    return dist*min(s.x, min(s.y, s.z));
}
//mirroring (doesn't work for some reason)
vec3 iSymX(vec3 p){
    p.x = abs(p.x);
    return p;
}
vec3 iSymY(vec3 p){
    p.y = abs(p.y);
    return p;
}
vec3 iSymZ(vec3 p){
    p.z = abs(p.z);
    return p;
}
vec3 iSymXZ(vec3 p){
    p.xz = abs(p.xz);
    return p;
}
// own
float oDisp(float dist, float disp){
    return dist - disp;
}
float deManySpheres(vec3 pos){
    pos.xy = mod((pos.xy),1.0)-vec2(0.5);
    return length(pos)-0.4;
}
float deMandelbulb(vec3 pos, int Iterations, float Bailout) {
    //    float t = fract(0.01*(uTime+15));
    //    float y = 16.0*t*(1.0-t);
    //    float maxp = 8;
    //    float Power = -2*maxp*abs(t-0.5)+maxp;
    float Power = 4;
    vec3 z = pos;
    float dr = 1.0;
    float r = 0.0;
    for (int i = 0; i < Iterations ; i++) {
        r = length(z);
        if (r>Bailout) break;
        float theta = asin( z.z/r );
        float phi = atan( z.y,z.x );
        dr =  pow( r, Power-1.0)*Power*dr + 1.0;

        float zr = pow( r,Power);
        theta = theta*Power;
        phi = phi*Power;
        z = zr*vec3( cos(theta)*cos(phi), cos(theta)*sin(phi), sin(theta) );
        z+=pos;
    }
    return 0.5*log(r)*r/dr;
}
float deJulia(vec3 p, int Iterations, float Bailout) {
    vec3 c = vec3(0.4,-0.4,0.6);
    //    float maxp = 8;
    //    float t = fract(0.01*(uTime+15));
    //    float power = -2*maxp*abs(t-0.5)+maxp;
    float power = 8;

    vec3 orbit = p;
    float dz = 1.0;

    for (int i=0; i<Iterations; i++) {

        float r = length(orbit);
        float o = acos(orbit.z/r);
        float p = atan(orbit.y/orbit.x);

        dz = power*pow(r,power-1)*dz;

        r = pow(r,power);
        o = power*o;
        p = power*p;

        orbit = vec3( r*sin(o)*cos(p), r*sin(o)*sin(p), r*cos(o) ) + c;

        if (dot(orbit,orbit) > Bailout) break;
    }
    float z = length(orbit);
    return 0.5*z*log(z)/dz;
}
float sdEnterprise(vec3 pos){
    return sbU(sbU(sbU(sbU(sbU(sbU(sbU(oBevel(sdCappedCylinder(pos, 1, 0.04), 0.02),sdEllipsoid(pos, vec3(0.7,0.2, 0.7)), 0.1),
    sdCappedCylinder(iTrans(pos, mat4(0, -1, 0, 0, 1, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 1))+vec3(-0.8, -1.1, 0), 0.2, 0.8), 0.01),
    sdCappedCylinder(iTrans(pos, mat4(0, -1, 0, 0, 1, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 1))+vec3(0, -2, -0.8), 0.1, 0.8), 0.01),
    sdCappedCylinder(iTrans(pos, mat4(0, -1, 0, 0, 1, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 1))+vec3(0, -2, 0.8), 0.1, 0.8), 0.01),
    sdBox(iTrans(pos, mat4(1, 0, 0, 0, 0, cos(pi/4), -sin(pi/4), 0, 0, sin(pi/4), cos(pi/4), 0, 0, 0, 0, 1))+vec3(-1.6, 0, 0.6), vec3(0.1,0.6,0.05)), 0.01),
    sdBox(iTrans(pos, mat4(1, 0, 0, 0, 0, cos(-pi/4), -sin(-pi/4), 0, 0, sin(-pi/4), cos(-pi/4), 0, 0, 0, 0, 1))+vec3(-1.6, 0, -0.6), vec3(0.1,0.6,0.05)), 0.01),
    sdBox(iTrans(pos, mat4(cos(-pi/5), -sin(-pi/5), 0, 0, sin(-pi/5), cos(-pi/5), 0, 0, 0, 0, 1, 0, 0, 0, 0, 1))+vec3(-0.2,0.6,0), vec3(0.2,0.5,0.05)),0.01);
}
float sdWarpTunnel(vec3 pos){
    pos = iTrans(pos, mat4(0, -1, 0, 0, 1, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, 1));
    return -sdInfinteCylinder(pos, vec3(0,0,10));
}
float test(vec3 pos){
    //    float t = -2*abs(fract(0.1*(uTime+15))-0.5)+1;
    return oMorph(sdBox(pos,  vec3(1,1,1)), sdSphere(pos, 1), 0.5);
}
float deKaliRemix(vec3 p, int Iterations) {
    float Scale=1.2;
    vec3 Julia=vec3(-.2,-1.95,-.6);

    p=p.zxy;  //more natural rotation
    float alpha1 = 20;
    float alpha2 = 20;
    mat3 rot = mat3(cos(alpha1/180*3.14),-sin(alpha1/180*3.14),0,sin(alpha1/180*3.14),cos(alpha1/180*3.14),0,0,0,1)*mat3(cos(alpha2/180*3.14), 0, -sin(alpha2/180*3.14), 0, 1, 0, sin(alpha2/180*3.14), 0, cos(alpha2/180*3.14));

    for (int i=0; i<Iterations; i++) {
        p.xy=abs(p.xy);
        p=p*Scale+Julia;
        p*=rot;
    }
    return (length(p))*pow(Scale, -float(Iterations))*.9;
}

float distObj(object obj, vec3 pos){
    switch(obj.obj_index){
        case 0:
        return sdBox(pos,  vec3(1,1,1));
        case 1:
        //          return sdEllipsoid(pos,  vec3(obj.scale,obj.scale,obj.scale));
        return sdSphere(pos,  obj.scale);
        case 2:
        return deJulia(pos, 30, 10);
        case 3:
        return sdEnterprise(pos/obj.scale)*obj.scale;
        case 4:
        return deKaliRemix(pos, 10);
        case 5:
        return deManySpheres(pos);
        case 6:
        return test(pos);
        case 7:
        return sbU(oBevel(sdBox(pos,  vec3(1,1,0.5)),0.01), sdEllipsoid(pos,  vec3(1,1,1)), 0.01);
        case 8:
        return sdPlane(pos, vec3(0,1,0),1);
        default:
        return 1.0/0.0; // maximal highp float
    }
}

hit map(vec3 pos){
    pos = iTrans(pos, viewMat);
    hit h = hit(false, 1.0/0.0, 0, 0);
    for (int i = 0; i < world.length(); ++i){
        float dist = oScale(distObj(world[i], iScale(inverse(world[i].rotation)*pos, world[i].scaling)+world[i].position), world[i].scaling);
        if (dist < h.dist){
            h = hit(false, dist, 0, i);
        }
    }
    return h;
}

//bool in_intersection(vec3 pos){
//    pos = iTrans(pos, viewMat);
//    bool first_hit = false;
//    for (int i = 0; i < world.length(); ++i){
//        float dist = oScale(distObj(world[i], iScale(inverse(world[i].rotation)*pos, world[i].scaling)+world[i].position), world[i].scaling);
//        if (dist < world[i].eps) {
//            if (first_hit) return true;
//            first_hit = true;
//        }
//    }
//    return false;
//}
//
//vec3 intersection_emmission(vec3 pos){
//    pos = iTrans(pos, viewMat);
//    vec3 emmission = vec3(0);
//    for (int i = 0; i < world.length(); ++i){
//        float dist = oScale(distObj(world[i], iScale(inverse(world[i].rotation)*pos, world[i].scaling)+world[i].position), world[i].scaling);
//        if (dist < world[i].eps) {
//            emmission += world[i].material.emmission;
//        }
//    }
//    return emmission;
//}

hit ray(vec3 ro, vec3 rd){
    hit h = hit(false, 0., 0, -1);
    for (; h.steps < steps; ++h.steps){
        hit d = map(ro+h.dist*rd);
        h.dist += d.dist;
        h.index = d.index;
        if (d.dist < world[d.index].eps) {
            h.hit = true;
            return h;
        }
        if (h.dist > FAR_PLANE) {
            return h;
        }
    }
    h.hit = false; // aproximates object. Might be innacurate but as can be let in as long it doesnt cause problems
    return h;
}

vec3 numDiff(vec3 p, float e){
    vec2 h = vec2(e,min(int(uFrame),0));
    return normalize(vec3(map(p+h.xyy).dist - map(p-h.xyy).dist, map(p+h.yxy).dist - map(p-h.yxy).dist, map(p+h.yyx).dist - map(p-h.yyx).dist));
}
vec3 tetraNormUnfix(vec3 p, float h){ // for function f(p){
    //    const float h = eps; // replace by an appropriate value
    const vec2 k = vec2(1,-1);
    return normalize( k.xyy*map( p + k.xyy*h ).dist +
    k.yyx*map( p + k.yyx*h ).dist +
    k.yxy*map( p + k.yxy*h ).dist +
    k.xxx*map( p + k.xxx*h ).dist );
}
vec3 tetraNorm(vec3 pos, float h){
    //    const float h = eps;      // replace by an appropriate value
    int ZERO = min(int(uFrame),0); // non-constant zero
    vec3 n = vec3(0.0);
    for( int i=ZERO; i<4; i++) {
        vec3 e = 0.5773*(2.0*vec3((((i+3)>>1)&1),((i>>1)&1),(i&1))-1.0);
        n += e*map(pos+e*h).dist;//.x;
    }
    return normalize(n);
}
vec3 calcNormal(vec3 p, object obj){
    //    int n = 32;
    //    vec3 nor = vec3(0,0,0);
    //    for (int i=0; i< n; ++i){
    //        nor += tetraNorm(p, eps*rand(i*2565));
    //    }
    //    return nor/n;
    return tetraNorm(p,obj.eps);
    //    return numDiff(p,obj.eps);
    //    return numDiff(p);
}

vec3 mapping(vec3 p, object obj){
    //Sphere UV project:
//    p = iTrans(p, viewMat);
//    float theta = acos(-p.y);
//    float phi = atan2(-p.z, p.x)+pi;
//    float u = phi / (2*pi);
//    float v = theta / pi;
//    return vec3(u,v,0);
    // normal
    return inverse(mat3(viewMat))*calcNormal(p, obj);
}

float cdot(vec3 a, vec3 b) {
    return clamp(dot(a,b), 0.0, 1.0);
}
float beckmannDistribution(float dotNH, float roughness) {
    float sigma2 = roughness * roughness;
    float alpha = acos(dotNH);

    // TASK: Compute d-term
    return (exp(-((tan(alpha) / sigma2) * (tan(alpha) / sigma2))) / (pi * sigma2 * pow(cos(alpha), 4)));
}
float schlickApprox(float dotVH, float n1, float n2) {
    float r0 = (n1 - n2) / (n1 + n2);
    float r0squared = r0 * r0;

    // TASK: Compute f-term
    return r0squared + (1 - r0squared) * pow(1 - dotVH, 5);
}
float geometricAttenuation(float dotNH, float dotVN, float dotVH, float dotNL) {
    // TASK: Compute g-term
    return min(1, min((2 * dotNH * dotVN) / (dotVH), (2 * dotNH * dotNL) / dotVH));
}
float cooktorranceTerm(vec3 n, vec3 l, float roughness, float refractionIndex) {
    vec3 v = vec3(0.0, 0.0, 1.0); // in eye space direction towards viewer simply is the Z axis
    vec3 h = normalize(l + v); // half-vector between V and L

    // precompute to avoid redundant computation
    float dotVN = cdot(v, n);
    float dotNL = cdot(n, l);
    float dotNH = cdot(n, h);
    float dotVH = cdot(v, h);

    float D = beckmannDistribution(dotNH, roughness);
    float F = schlickApprox(dotVH, 1.0, refractionIndex);
    float G = geometricAttenuation(dotNH, dotVN, dotVH, dotNL);

    return max(D * F * G / (4.0 * dotVN * dotNL), 0.0);
}
vec3 projected(vec3 vec, vec3 normal) {
    return normalize(vec - dot(vec, normal) * normal);
}
float orennayarTerm(float lambert, vec3 n, vec3 l, float roughness) {
    vec3 v = vec3(0.0, 0.0, 1.0);
    float sigma2 = roughness * roughness;

    float a = 1 - 0.5 * (sigma2 / (sigma2 + 0.57));
    float b = 0.45 * (sigma2 / (sigma2 + 0.09));
    float alpha = max(acos(dot(l, n)), acos(dot(v, n)));
    float beta = min(acos(dot(l, n)), acos(dot(v, n)));
    return lambert * (a + (b * cdot(projected(l, n), projected(v, n)) * sin(alpha) * tan(beta)));
}

float brdf(mat material, vec3 normal, vec3 dir_in, vec3 dir_out){
    return 0.5;
    //    return cdot(normal, dir_in);
}

vec3 sky(vec3 direction){
    float t = 0.5*(direction.y + 1.0);
    return vec3(1.-t) + t*vec3(0.5, 0.7, 1.0);
    //    return iTrans(direction, viewMat);
    //    return vec3(1);
    //    return vec3(0.);
}

struct path {
    bool stop;
    vec3 attenuation;
    vec3 direction;
    vec3 emmission;
};

path add(path old, path new){
    return path(old.stop||new.stop,old.attenuation*new.attenuation,new.direction,old.emmission+new.emmission*old.attenuation);
}

vec3 reflect(vec3 v, vec3 n) {
    return v - 2*dot(v,n)*n;
}

path scatter(mat material, vec3 p, vec3 normal, vec3 dir_in){
    if(material.type == 0) {  //lambertian
        vec3 new_direction =  normalize(normalize(normal) + rand_unit_sphere());
        return path(false, material.albedo, new_direction, vec3(0));
    }
    if (material.type == 1){ //metal
        vec3 new_direction = normalize(normalize(reflect(dir_in, normal)) + material.fuzz*rand_in_unit_sphere());
        return path(dot(new_direction, normal) <= 0, material.albedo, new_direction, vec3(0));
    }
    if (material.type == 2){ //emmission
        return path(true, vec3(1), vec3(0), material.emmission);
    }
    return path(true, vec3(0),vec3(0),vec3(0));
}

vec3 pathtracing(vec3 origin, vec3 direction){
    path trace = path(false, vec3(1), direction, vec3(0));
    for (int i = 0; i < max_bounce; ++i){
        hit r = ray(origin, trace.direction);
        if (r.hit){
            object obj = world[r.index];
            vec3 p = origin + r.dist*trace.direction;
            //            if (in_intersection(p)) return trace.emmission+intersection_emmission(p);
            vec3 normal = calcNormal(p, obj);
            //            return inverse(mat3(viewMat))*normal;
            trace = add(trace, scatter(obj.material, p, normal, trace.direction));
            origin = p+2*obj.eps*normalize(trace.direction+normal);

            //            float theta = acos(-p.y);
            //            float phi = atan2(-p.z, p.x)+pi;
            //            float u = phi / (2*pi);
            //            float v = theta / pi;
            //            origin = p;
            //            if (map(origin).dist < obj.eps) return vec3(0,1,0);
            //            if (map(origin, trace.direction) < obj.eps) origin += 2*obj.eps*normal;
        }
        else {
            //            vec3 col = refl*sky(iTrans(trace.direction, inverse(viewMat)));
            vec3 col = trace.emmission + trace.attenuation*sky(iTrans(trace.direction, inverse(viewMat)));
            return col;
            //            emit_accum += attenuation_accum*sky(iTrans(trace.direction, inverse(viewMat)));
            //            return emit_accum;
        }
        if (trace.stop) return trace.emmission;
    }
    return vec3(0);
}

vec3 getRayDir(vec2 p, float aspect){
    float sensor_width = 35; // sensor width is identic to Blender Camera
    float FOV = 2 * atan(sensor_width/(2*focal_length));  // in rad
    float renderWidth = 2;
    float z0 = renderWidth / (2 * tan(FOV / 2));
    p.x *= aspect;
    return normalize(vec3(p.xy,-z0));
}

void main(){

    uvec2 texelCoord = ivec2(tileOffset * workGroupSize * workGroupCount + gl_GlobalInvocationID.xy);
    ivec2 resolution = imageSize(hdrOut);

    if (texelCoord.x >= resolution.x || texelCoord.y >= resolution.y)
    return;

    for (int i = 0; i < world.length(); ++i){
        world[i].scale /= world_scale;
        world[i].position /= world_scale;
    }

    vec3 light_dir = vec3(cos(light_phi)*sin(light_theta),cos(light_theta),sin(light_phi)*sin(light_theta));
    vec3 interp_light_dir = normalize((viewMat * vec4(light_dir, 0.0)).xyz);

    vec2 p = (vec2(texelCoord) / resolution) * 2 - 1;
    float aspect = float(resolution.x) / float(resolution.y);

    vec4 col = vec4(.1,.1,.1,1);

    // for noise testing
    //    col = vec4(0,0,0,1);
    //    for (int i = 0; i <10; ++i){
    //        col.xyz += vec3(rand());
    //    }
    //    col.xyz /= 10.;
    //    imageStore(hdrOut, ivec2(texelCoord), col);
    //    return;

    if (preview) {
        col = vec4(float(currentSample) + float(samplesPerPass) - float(currentSample) - float(samplesPerPass) + 0.1);
        vec3 rd = getRayDir(p, aspect);
        hit r = ray(ro, rd);
        float t = r.dist;
        //    col = float(r.steps)/float(steps)*vec4(1,1,1,1);
        if (r.hit){
            object obj = world[r.index];
            vec3 pos = ro + t*rd;
            vec3 nor = calcNormal(pos, obj);
            float diffuseTerm = cdot(nor, interp_light_dir);
            diffuseTerm = orennayarTerm(diffuseTerm, nor, interp_light_dir, obj.material.roughness);
            diffuseTerm = max(diffuseTerm, 0.1);
            float specularTerm = cooktorranceTerm(nor, interp_light_dir, obj.material.roughness, obj.material.refractionIndex);
            //            col = clamp(vec4(obj.material.diff_col,1) * diffuseTerm + vec4(obj.material.spec_col,1) * specularTerm, 0.0, 1.0);

            vec3 diff_col = vec3(0);
            vec3 map = mapping(pos, obj);
            //            diff_col = vec3(fade(.1));
            //            diff_col = vec3(perlin_noise_p(map.x,map.y,map.z));
            float val;
            vec4 coll;
            node_noise_texture_3d(map, 1., 2., 2., 1, 5., val, coll);
            diff_col = vec3(val);

            col = clamp(vec4(diff_col,1) * diffuseTerm + vec4(obj.material.spec_col,1) * specularTerm, 0.0, 1.0);

            //        hit tmp = ray(pos+nor*eps(t)*2, interp_light_dir);
            hit tmp = ray(pos+nor*obj.eps*2, interp_light_dir);
            float TMP = (tmp.hit) ? tmp.dist : 0;
            float sun_sh = step(TMP, 0.0);
            vec4 shadow = vec4(1, 1, 1, 1)*clamp(10*pow(0.18*sun_sh, 0.4545), 0.4, 1.);
            //            col = col*shadow;

            //            col = world[r.index].diff_col;
            //            col = vec4(inverse(mat3(viewMat))*nor,1);
//            col = vec4(diff_col,1);
        }
    }
    else {
        vec3 pathTraceCol = vec3(0);
        for (int i = 0; i < samplesPerPass; i++) {
            vec3 rd = getRayDir(p+vec2(2.*rand()-1.,2.*rand()-1.)/resolution.y,aspect);
            pathTraceCol += pathtracing(ro, rd);
        }
        vec4 pcol = vec4(pathTraceCol,1);
        col = imageLoad(hdrOut, ivec2(texelCoord));
        col.xyz = (float(currentSample) / float(currentSample + samplesPerPass)) * col.xyz + pcol.xyz / float(currentSample + samplesPerPass);
    }

    imageStore(hdrOut, ivec2(texelCoord), col);
}